plugins {
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
}

group = 'io.honeycomb.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    distroVersion = '1.4.2'
    otelAgentVersion = '1.28.0'

}

configurations {
    agent
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelAgentVersion}"

    implementation("io.honeycomb:honeycomb-opentelemetry-sdk:${distroVersion}")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}


task copyAgentJar(type: Copy) {
    from configurations.agent {
        include '**/opentelemetry-javaagent*.jar'
    }
    into "agent"
    rename { fileName -> "otel-javaagent.jar" }
}

compileJava.dependsOn copyAgentJar
bootRun.dependsOn copyAgentJar

bootRun.doFirst {
    jvmArgs("-javaagent:agent/otel-javaagent.jar")
}

clean.doFirst {
    delete "agent"
}

test {
    useJUnitPlatform()
}

